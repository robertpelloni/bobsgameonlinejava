import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

project.ext.lwjglVersion = "3.3.3"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-jemalloc"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-jemalloc::$lwjglNatives"

    // Local JARs
    implementation files('libs/TWL.jar')
    implementation files('libs/TWLEffects.jar')
    implementation files('libs/ibxm77.jar')

    // Dependencies from pom.xml
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.googlecode.soundlibs:jorbis:0.0.17.4'
    implementation 'org.slf4j:slf4j-api:2.0.3'
    implementation 'org.slf4j:jcl-over-slf4j:2.0.3'
    implementation 'org.slf4j:log4j-over-slf4j:2.0.3'
    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation 'commons-net:commons-net:3.8.0'
    implementation 'io.netty:netty:3.10.6.Final'
    implementation 'com.restfb:restfb:2022.10.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-compress:1.22'
    implementation 'com.ibm.icu:icu4j:72.1'
    implementation 'com.mchange:c3p0:0.9.5.5'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'com.mysql:mysql-connector-j:8.0.31'
    implementation 'org.apache.commons:commons-email:1.5'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
    implementation 'org.jasypt:jasypt:1.9.3'
    implementation 'org.lz4:lz4-java:1.8.0'
    implementation 'com.maxmind.geoip2:geoip2:3.0.2'
    implementation 'net.java.jinput:jinput:2.0.9'
    implementation 'xpp3:xpp3:1.1.4c'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'libs/twl-lwjgl3/src']
        }
        resources {
            srcDirs = ['src/main/resources', 'res']
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.bobsgame.ClientMain'
}
